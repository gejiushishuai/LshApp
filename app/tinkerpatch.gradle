//apply tinker插件
apply plugin: 'com.tencent.tinker.patch'

def bakPath = file("${buildDir}/bakApk/")

def baseInfo = "app-1.0-0512-15-14-37"

def variantName = "debug"

def _tinkerId = "1.0"

def tinkerEnabled = true

def tinkerOldApkPath = "${bakPath}/${baseInfo}/${variantName}/${project.name}-${variantName}.apk" // 旧APK地址

def tinkerApplyMappingPath = "${bakPath}/${baseInfo}/${variantName}/${project.name}-${variantName}-mapping.txt" // 旧Mapping文件地址

def tinkerApplyResourcePath = "${bakPath}/${baseInfo}/${variantName}/${project.name}-${variantName}-R.txt" // 旧R文件地址

def date = new Date().format("MMdd-HH-mm-ss")

def gitSha() {
    try {
        String gitRev = 'git rev-parse --short HEAD'.execute(null, project.rootDir).text.trim()
        println("-------gitRev-----" + gitRev)
        if (gitRev == null) {
            throw new GradleException("can't get git rev, you should add git to system path or just input test value, such as 'testTinkerId'")
        }
        return gitRev
    } catch (Exception e) {
        throw new GradleException("can't get git rev, you should add git to system path or just input test value, such as 'testTinkerId'")
    }
}

tinkerPatch {

    oldApk = tinkerOldApkPath

    ignoreWarning = false

    useSign = true

    tinkerEnable = tinkerEnabled

    buildConfig {

        applyMapping = tinkerApplyMappingPath

        applyResourceMapping = tinkerApplyResourcePath

        tinkerId = "debug".equals(variantName) ? date : gitSha();

        keepDexApply = false

        isProtectedApp = false
    }

    dex {

        dexMode = "jar"

        pattern = ["classes*.dex",
                   "assets/secondary-dex-?.jar"]
        loader = [
                //use sample, let BaseBuildInfo unchangeable with tinker
                "tinker.sample.android.app.BaseBuildInfo"
        ]
    }

    lib {
        pattern = ["lib/*/*.so"]
    }

    res {
        pattern = ["res/*", "assets/*", "resources.arsc", "AndroidManifest.xml"]

        ignoreChange = ["assets/sample_meta.txt"]

        largeModSize = 100
    }

    packageConfig {

        configField("patchMessage", "tinker is sample to use")

        configField("platform", "all")

        configField("patchVersion", "1.0")
    }
    //or you can add config filed outside, or get meta value from old apk
    //project.tinkerPatch.packageConfig.configField("test1", project.tinkerPatch.packageConfig.getMetaDataFromOldApk("Test"))
    //project.tinkerPatch.packageConfig.configField("test2", "sample")

    sevenZip {

        zipArtifact = "com.tencent.mm:SevenZip:1.1.10"

//        path = "/usr/local/bin/7za"
    }
}

/**
 * bak apk and mapping
 */
android.applicationVariants.all { variant ->
    /**
     * task type, you want to bak
     */
    def taskName = variant.name

    tasks.all {
        if ("assemble${taskName.capitalize()}".equalsIgnoreCase(it.name)) {

            it.doLast {
                copy {
                    def pathPrefix = "${bakPath}/${project.name}-${_tinkerId}-${date}/${variantName}/"
                    def name = "${project.name}-${variantName}"

                    def destPath = file(pathPrefix)
                    from variant.outputs.outputFile
                    into pathPrefix
                    rename { String fileName ->
//                        fileName.replace("${fileNamePrefix}.apk", "${newFileNamePrefix}.apk") // 该语句不适用, 因为我修改了APK的名字
                        fileName.replace("LshApp_" + "v${variant.versionName}.apk", "${name}.apk")
                    }

                    from "${buildDir}/outputs/mapping/${variant.dirName}/mapping.txt"
                    into destPath
                    rename { String fileName ->
                        fileName.replace("mapping.txt", "${name}-mapping.txt")
                    }

                    from "${buildDir}/intermediates/symbols/${variant.dirName}/R.txt"
                    into destPath
                    rename { String fileName ->
                        fileName.replace("R.txt", "${name}-R.txt")
                    }
                }
            }
        }
    }
}