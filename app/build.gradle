apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    signingConfigs {
        release {
            //加载资源
            Properties properties = new Properties()
            InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
            properties.load(inputStream)
            //读取文件
            def sdkDir = properties.getProperty('storeFile')
            storeFile file(sdkDir)
            //读取字段
            def key_keyAlias = properties.getProperty('keyAlias')
            def key_keyPassword = properties.getProperty('keyPassword');
            def key_storePassword = properties.getProperty('storePassword');

            storePassword key_storePassword
            keyAlias key_keyAlias
            keyPassword key_keyPassword
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.linsh.lshapp"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 11
        versionName "1.1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }
    // 控制打包apk的文件名
    applicationVariants.all {
        variant ->
            variant.outputs.each { output ->
                output.outputFile = new File(
                        output.outputFile.parent,
                        output.outputFile.name.replace(output.outputFile.name, "LshApp_" + "v${variant.versionName}") + ".apk")
            }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    ////////// 官方
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    testCompile 'junit:junit:4.12'
    //////////  常用
    // glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    // butterknife
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    // 汉字转拼音
    compile 'com.belerweb:pinyin4j:2.5.1'
    // Rxjava
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    // Dagger
    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    // Multidex
    compile 'com.android.support:multidex:1.0.1'
    // Tinker
    provided 'com.tencent.tinker:tinker-android-anno:1.7.9'
    compile 'com.tencent.tinker:tinker-android-lib:1.7.9'
    // 通讯录
    compile 'com.github.yuanxiang05:Contacts:1.0.3'
    // PhotoView
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    // PickerView
    compile 'cn.carbswang.android:NumberPickerView:1.1.1'

    //////////  个人
    compile project(':LshUtils:lshutils')
}

apply from: 'tinkerpatch.gradle'